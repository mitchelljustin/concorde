//
// Created by intellij-pest on 2023-08-27
// concorde
// Author: mitch
//

program = {
    stmts
}

stmts = {
    (stmt ~ NEWLINE)+
}

optional_stmts = {
    (stmt ~ NEWLINE)*
}

stmt = {
    assignment |
    method_def |
    class_def |
    expr
}

method_def = {
    "def" ~ ident ~ param_list ~ NEWLINE ~
        optional_stmts ~
    "end"
}

class_def = {
    "class" ~ ident ~ NEWLINE ~
        optional_stmts ~
    "end"
}

param_list = {
    ("(" ~ ")") |
    ("(" ~ param ~ ("," ~ param)* ~ ","? ~ ")")
}

param = {
    ident
}

assignment = {
    lvalue ~ "=" ~ expr
}

expr = {
    access
}
// a.b -> access(ident, ident)
// a.b() -> call(access(ident, ident))
// a().b() -> call(access(call(ident), ident))
access = {
    call ~ ("." ~ call)*
}

call = {
    primary ~ expr_list?
}

primary = {
    literal |
    grouping |
    variable
}

grouping = {
    "(" ~ expr ~ ")"
}

lvalue = {
    access
}

variable = {
    ident
}

expr_list = {
    ("(" ~ ")") |
    ("(" ~ expr ~ ("," ~ expr)* ~ ","? ~")")
}

literal = {
    number | string | bool | nil
}

bool = @{ "true" | "false" }
nil = @{ "nil" }

KEYWORD = @{
    "class" | "true" | "false" | "nil" | "def" | "end"
}

ident = @{
    !(KEYWORD ~ !XID_START) ~
    (XID_START | "_") ~ XID_CONTINUE*
}

number = @{
    "-"? ~
    integer ~
    ("." ~ mantissa)?
}
integer = _{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = _{ ASCII_DIGIT+ }

string = @{
    "\"" ~ string_inner ~ "\""
}

string_inner = ${(!"\"" ~ ANY)*}

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

