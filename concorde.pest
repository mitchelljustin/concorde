//
// Created by intellij-pest on 2023-08-27
// concorde
// Author: mitch
//

program = {
    stmts_required
}

stmts_required = {
    (stmt? ~ NEWLINE)+
}

stmts = {
    (stmt? ~ NEWLINE)*
}

stmt = {
    assignment |
    method_def |
    class_def |
    expr
}

method_def = {
    "def" ~ ident ~ param_list ~ NEWLINE ~
        stmts ~
    "end"
}

class_def = {
    "class" ~ ident ~ NEWLINE ~
        stmts ~
    "end"
}

param_list = {
    ("(" ~ ")") |
    ("(" ~ param ~ ("," ~ param)* ~ ","? ~ ")")
}

param = {
    ident
}

assignment = {
    lvalue ~ "=" ~ expr
}

expr = {
    if_else |
    logical_or
}

logical_or = {
    logical_and ~ (op_or ~ logical_and)*
}

logical_and = {
    equality ~ (op_and ~ equality)*
}

equality = {
    comparison ~ (op_equality ~ comparison)*
}

op_equality = _{
    op_eq | op_neq
}

comparison = {
    term ~ (op_comparison ~ term)*
}

op_comparison = _{
    op_gt | op_gte | op_lt | op_lte
}

term = {
    factor ~ (op_term ~ factor)*
}

op_term = _{ op_plus | op_minus }

factor = {
    logical_not ~ (op_factor ~ logical_not)*
}

op_factor = _{ op_star | op_slash }

logical_not = {
    op_not* ~ unary_minus
}

unary_minus = {
    op_minus* ~ access
}

op_eq = { "==" }
op_neq = { "!=" }
op_gt = { ">" }
op_gte = { ">=" }
op_lt = { "<" }
op_lte = { "<=" }
op_minus = { "-" }
op_plus = { "+" }
op_star = { "*" }
op_slash = { "/" }
op_not = { "not" }
op_or = { "or" }
op_and = { "and" }

// or
// and
// == !=
// < > <= >=
// + -
// * /
// not
// (-)
// primary

if_else = {
    "if" ~ logical_or ~ NEWLINE ~
        stmts ~
    ("else" ~ NEWLINE ~
        stmts)? ~
    "end"
}

// a.b -> access(ident, ident)
// a.b() -> call(access(ident, ident))
// a().b() -> call(access(call(ident), ident))
access = {
    call ~ ("." ~ call)*
}

call = {
    primary ~ expr_list?
}

primary = {
    literal |
    grouping |
    variable
}

grouping = {
    "(" ~ expr ~ ")"
}

lvalue = {
    access
}

variable = {
    ident
}

expr_list = {
    ("(" ~ ")") |
    ("(" ~ expr ~ ("," ~ expr)* ~ ","? ~")")
}

literal = {
    number | string | bool | nil
}

bool = @{ "true" | "false" }
nil = @{ "nil" }

KEYWORD = @{
    "class" | "true" | "false" | "nil" | "def" | "end" | "if" | "else"
}

ident = @{
    !(KEYWORD ~ !XID_START) ~
    (XID_START | "_") ~ XID_CONTINUE*
}

number = @{
    "-"? ~
    integer ~
    ("." ~ mantissa)?
}
integer = _{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = _{ ASCII_DIGIT+ }

string = @{
    "\"" ~ string_inner ~ "\""
}

string_inner = ${(!"\"" ~ ANY)*}

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }

